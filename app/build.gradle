import cn.lliiooll.dhl.plugin.Common

plugins {
    id 'com.android.application'
    id 'cn.lliiooll.dhl.plugin'
}

android {
    namespace 'cn.lliiooll.pphelper'
    compileSdk 33
    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false
    defaultConfig {
        applicationId "cn.lliiooll.pphelper"
        minSdk 26
        targetSdk 33
        versionCode Common.getBuildVersionCode(rootProject)
        versionName "2.0${Common.getGitHeadRefsSuffix(rootProject)}"
        resConfigs "zh"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = false
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"

        ndk {
            abiFilters /*"armeabi-v7a",*/ "arm64-v8a"
        }

        externalNativeBuild {
            cmake {
                targets "pphelper"
                arguments "-DANDROID_STL=c++_static"
                cppFlags "-std=c++17", "-Wall",
                        "-Qunused-arguments",
                        "-Wno-unused-value",
                        "-Wno-unused-variable",
                        "-Wno-unused-command-line-argument"
                cFlags "-std=c18", "-Wall",
                        "-Qunused-arguments",
                        "-Wno-unused-value",
                        "-Wno-unused-variable",
                        "-Wno-unused-command-line-argument"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding false
    }

    aaptOptions {
        additionalParameters("--no-version-vectors", "--allow-reserved-package-id", "--package-id", "0xcf")
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    packagingOptions {
        jniLibs.excludes.add("lib/**/liblog.so")
        jniLibs.excludes.add("lib/**/libz.so")
        jniLibs.excludes.add("lib/**/libmmkv.so")
        resources.excludes.addAll(
                "META-INF/**",
                "kotlin/**",
                "**.bin",
                "kotlin-tooling-metadata.json"
        )
    }

    lint {
        checkDependencies = true
    }
}

dependencies {

    //implementation 'com.google.android.material:material:1.7.0'
    //implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation project(":dexkit")
    compileOnly 'de.robv.android.xposed:api:82'

    implementation 'com.google.code.gson:gson:2.10'
}